apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.simplemobiletools.gallery"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 203
        versionName "5.0.1"
        multiDexEnabled true
        setProperty("archivesBaseName", "gallery")
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.github.MoKee:android_packages_apps_SimpleCommons:5.1.6.1'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'it.sephiroth.android.exif:library:1.0.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.15'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.9.0'
    implementation 'com.google.vr:sdk-panowidget:1.170.0'
    implementation 'org.apache.sanselan:sanselan:0.97-incubator'
    implementation 'info.androidhive:imagefilters:1.0.7'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.caverock:androidsvg-aar:1.3'
    kapt 'com.github.bumptech.glide:compiler:4.8.0' // keep it here too, not just in Commons, else loading SVGs wont work

    kapt "androidx.room:room-compiler:2.0.0"
    implementation "androidx.room:room-runtime:2.0.0"
    annotationProcessor "androidx.room:room-compiler:2.0.0"

    //implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.github.tibbi:subsampling-scale-image-view:v3.10.1-fork'

    // implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'com.github.tibbi:PhotoView:2.2.1-fork'
}

Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('KEY_ALIAS') && props.containsKey('PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
